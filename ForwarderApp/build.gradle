plugins {

    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
}

android {

    namespace 'in.radongames.smsforwarder'

    defaultConfig {

        compileSdk findProperty("androidsdk.version.compile") as Integer
        targetSdk findProperty("androidsdk.version.target") as Integer
        minSdk findProperty("androidsdk.version.minimum") as Integer

        def versionBase = 10101
        def productName = "ForwarderApp"

        applicationId "in.radongames.smsforwarder"

        versionCode generateVersionAsInt(versionBase)
        versionName generateVersionAsString(versionBase)

        archivesBaseName = "${productName}-v${versionName}"

        // Set some globals.
        project.ext.productName = productName
        project.ext.productVersion = versionName
    }

    signingConfigs {

        master {

            // Filled in by allprojects.afterEvaluate
            // Has to be before buildTypes because signingConfigs is referred there.
        }

        debug {

            // This is needed for local debugging.
        }
    }

    buildTypes {

        debug {

            minifyEnabled false
            jniDebuggable true
            shrinkResources false
            signingConfig signingConfigs.master
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }

        release {

            minifyEnabled false
            jniDebuggable false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.master
            enableUnitTestCoverage false
            enableAndroidTestCoverage false
        }
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {

        resources.excludes.add("META-INF/DEPENDENCIES")
        resources.excludes.add("META-INF/INDEX.LIST")
        resources.excludes.add("META-INF/*.md")
        resources.excludes.add("META-INF/*.properties")
    }

    viewBinding {

        enabled = true
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':SmsLib')

    implementation 'com.radongames:mensa-core:1.2.23'
    implementation 'com.radongames:mensa-json:1.0.9'
    implementation 'com.radongames:mensa-android-lib:1.2.19'

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'com.google.android.material:material:1.12.0'

    implementation 'com.google.firebase:firebase-admin:9.3.0'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    annotationProcessor 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'com.google.dagger:hilt-android:2.52'
    annotationProcessor 'com.google.dagger:hilt-android-compiler:2.52'
}
